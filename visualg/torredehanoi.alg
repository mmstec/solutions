algoritmo "TORRE DE HANÓI"
var
DISCOS, MOVIMENTOS: INTEIRO
PROCEDIMENTO TRANSFERE (I:INTEIRO; P1, P2, P3:CARACTER)
   INICIO
   SE I=1 ENTAO
      MOVIMENTOS<-MOVIMENTOS + 1
      ESCREVAL (" MOVER DO PINO",P1 , " PARA O PINO" , P2)
   SENAO
      TRANSFERE (I-1, P1, P3, P2)
      MOVIMENTOS<-MOVIMENTOS+1
      ESCREVAL (" MOVER DO PINO", P1, " PARA O PINO", P2)
      TRANSFERE(I-1, P3, P2, P1)
   FIMSE
FIMPROCEDIMENTO

INICIO
   ESCREVAL("----------- DESAFIO --------------")
   ESCREVAL("")
   ESCREVA (" NÚMERO DE DISCOS: ")
   LEIA(DISCOS)
   ESCREVAL("")
   TRANSFERE (DISCOS, " 1 ", " 2 ", " 3 " )
   ESCREVAL("")
   ESCREVAL (MOVIMENTOS, " MOVIMENTOS")
FIMALGORITMO
